apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def ankoLibraryVersion = '0.10.3'
def androidSupportLibraryVersion = '27.0.2'

kapt {

}

android {
    compileSdkVersion 27
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "io.github.anthonyeef.cattle"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        if (System.getenv("TRAVIS")) {
            buildConfigField "String", "API_KEY", "\"YOUAPI_KEYHERE\""
            buildConfigField "String", "API_SECRET", "\"YOUAPI_SECRETHERE\""
        } else {
            buildConfigField("String", "API_KEY", API_KEY)
            buildConfigField("String", "API_SECRET", API_SECRET)
        }

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError true
        warning 'InvalidPackage'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'

    implementation "com.android.support:appcompat-v7:$androidSupportLibraryVersion"
    implementation "com.android.support:design:$androidSupportLibraryVersion"

    implementation "android.arch.persistence.room:runtime:1.0.0"
    kapt "android.arch.persistence.room:compiler:1.0.0"

    implementation "android.arch.persistence.room:rxjava2:1.0.0"

    implementation "android.arch.lifecycle:extensions:1.0.0"
    implementation "android.arch.lifecycle:reactivestreams:1.0.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "org.jetbrains.anko:anko-sdk25:$ankoLibraryVersion"
    implementation "org.jetbrains.anko:anko-sdk25-listeners:$ankoLibraryVersion"
    implementation "org.jetbrains.anko:anko-appcompat-v7:$ankoLibraryVersion"
    implementation "org.jetbrains.anko:anko-design:$ankoLibraryVersion"

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.1.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'

    implementation 'io.reactivex.rxjava2:rxjava:2.1.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

    implementation 'com.github.bumptech.glide:okhttp3-integration:4.3.0'
    implementation 'com.github.bumptech.glide:glide:4.4.0'
    kapt 'com.github.bumptech.glide:compiler:4.4.0'
    implementation 'de.hdodenhof:circleimageview:2.1.0'

    implementation 'me.drakeet.multitype:multitype:3.0.1', {
        exclude group: 'com.android.support'
    }

    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta4'

    implementation 'org.ocpsoft.prettytime:prettytime:4.0.1.Final'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
repositories {
    mavenCentral()
    google()
}
